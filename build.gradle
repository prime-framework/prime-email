apply plugin: 'java'

group = "org.primeframework"
version = "1.0-SNAPSHOT"
description = "Prime Email Framework"
sourceCompatibility = 1.6

configurations {
  compileOnly {
    visible = false
  }
  testCompile.extendsFrom compileOnly
}

repositories {
  mavenCentral()
}

dependencies {
  compileOnly(
    "javax.mail:mail:1.4.2"
  )

  compile(
    "com.google.inject:guice:3.0",
    "commons-fileupload:commons-fileupload:1.2.1",
    "commons-io:commons-io:1.3.2",
    "org.freemarker:freemarker:2.3.16"
  )

  testCompile(
    "org.easymock:easymock:3.1",
    "org.testng:testng:6.4"
  )
}

repositories {
  ivy {
    name = "ivyLocal"
    url "${project.gradle.gradleUserHomeDir}/integration-cache"
  }
}

uploadArchives {
  repositories {
    add project.repositories.ivyLocal
  }
}

// gradle doesn't provide the capability to define 'compile only'
// dependencies (e.g maven 'provided') so this script-block below adds it
sourceSets {
  main {
    compileClasspath += configurations.compileOnly
  }
}

task srcJar(type: Jar) {
  from sourceSets.main.java
  classifier = "sources"
}

artifacts {
  archives srcJar
}

test {
  useTestNG()
}